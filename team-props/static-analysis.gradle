apply plugin: 'com.novoda.static-analysis'

staticAnalysis {
    penalty {
        maxErrors = 54
        maxWarnings = 196
    }

    checkstyle {
        toolVersion '8.0'
        exclude project.fileTree('src/test/java')
        exclude project.fileTree('src/testDebug/java')
        configFile rootProject.file('team-props/static-analysis/checkstyle-modules.xml')
        includeVariants { variant -> excludeTestAndRelease(variant) }
    }

    pmd {
        toolVersion '5.8.1'
        exclude project.fileTree('src/test/java')
        exclude project.fileTree('src/testDebug/java')

        ruleSetFiles = rootProject.files('team-props/static-analysis/pmd-rules.xml')
        ruleSets = []   // Note: this is a workaround to make the <exclude-pattern>s in pmd-rules.xml actually work
        includeVariants { variant -> excludeTestAndRelease(variant) }
    }

    findbugs {
        toolVersion '3.0.1'
        excludeFilter rootProject.file('team-props/static-analysis/findbugs-excludes.xml')
        includeVariants { variant -> excludeTestAndRelease(variant) }
    }
}

static boolean excludeTestAndRelease(variant) {
    return !variant.name.toLowerCase().endsWith('test') && !variant.name.toLowerCase().endsWith('release')
}
