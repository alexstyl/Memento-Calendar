apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
//apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.neenbedankt.android-apt'

ext {
    android_support_version = '24.2.1'
}


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}



android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
        }
    }


    defaultConfig {
        applicationId 'com.alexstyl.specialdates'
        minSdkVersion 16
        targetSdkVersion 24
        versionCode androidVersionCode
        versionName androidVersionName
        manifestPlaceholders = [crashlyticsApiKey: crashlyticsKey]
        buildConfigField 'String', 'API_KEY_VENDING', '\"' + androidVendingKey + "\""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'http://dl.bintray.com/novoda/maven'
    }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', exclude: 'android-support-v4.jar', include: ['*.jar'])
    compile project(':common')
    compile "com.android.support:design:$android_support_version"
    compile "com.android.support:appcompat-v7:$android_support_version"
    compile "com.android.support:recyclerview-v7:$android_support_version"
    compile "com.android.support:transition:$android_support_version"
    compile "com.android.support:support-annotations:$android_support_version"
    compile 'net.danlew:android.joda:2.9.4.1'
    compile('de.psdev.licensesdialog:licensesdialog:1.5.0') {
        exclude module: 'support-v4'
    }
    compile 'com.novoda:notils:2.2.15'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.novoda:simple-chrome-custom-tabs:0.1.3'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.google.android.gms:play-services-wearable:9.6.1'
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    debugCompile 'com.facebook.stetho:stetho:1.3.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'joda-time:joda-time:2.9.4'
    testCompile 'org.json:json:20140107'

    androidTestCompile 'junit:junit:4.12'
}

// This must be applied after the dependencies closure in order to avoid conflicts ¯\_(ツ)_/¯
// see http://stackoverflow.com/questions/38127053/google-play-services-gcm-9-2-0-asks-to-update-back-to-9-0-0
apply plugin: 'com.google.gms.google-services'
